PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX voaf: <http://purl.org/vocommons/voaf#>
PREFIX vann: <http://purl.org/vocab/vann/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX cc:      <http://creativecommons.org/ns#>
PREFIX schema:  <http://schema.org/>

# ?term A term (class, property or datatype)
# ?vocab The vocabulary that defines the term, according to LOV metadata
# ?license The vocabulary's license (if known)
SELECT ?term ?vocab (COALESCE(?licLov, ?licGraph, ?licCC, ?licSchema) AS ?license) {

  GRAPH <LOV_DATASET_URI> {
    ?vocab a voaf:Vocabulary ;
           vann:preferredNamespaceUri ?ns .

    OPTIONAL { ?vocab dcterms:license ?licLov }
  }

  {
    SELECT DISTINCT ?vocab ?term ?licGraph ?licCC ?licSchema {
      GRAPH ?vocab {
        { ?term a rdfs:Class }
        UNION { ?term a owl:Class }
        UNION { ?term a rdf:Property }
        UNION { ?term a owl:DatatypeProperty }
        UNION { ?term a owl:ObjectProperty }
        UNION { ?term a owl:AnnotationProperty }
        UNION { ?term a rdfs:Datatype }
        UNION { ?term a owl:NamedIndividual . }

        FILTER isURI(?term)

        # Licencia declarada dentro del propio grafo del vocabulario (si existe)
        OPTIONAL { ?vocab dcterms:license ?licGraph }
        OPTIONAL { ?vocab cc:license ?licCC }
        OPTIONAL { ?vocab schema:license ?licSchema }
      }
    }
  }

  FILTER (STRSTARTS(STR(?term), STR(?ns)))
}
